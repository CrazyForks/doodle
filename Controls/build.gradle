apply from  : '../jacoco.gradle'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js,  'js' ) {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    sourceMap             = true
                    sourceMapEmbedSources = 'always'
                    moduleKind            = 'commonjs'
                }
            }
        }

        all {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    freeCompilerArgs = ["-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"]
//                    allWarningsAsErrors = true
                }
            }
        }
    }

    sourceSets {
        commonMain.dependencies {
            api project(':core' )
            implementation dep.kotlin_stdlib_common
        }

        commonTest.dependencies {
            implementation dep.kotlin_test_common
            implementation dep.kotlin_test_annotations_common
            implementation dep.mockk_common
        }

        jvmMain.dependencies {
            implementation dep.kotlin_stdlib_jdk8
        }

        jvmTest.dependencies {
            implementation dep.junit
            implementation dep.kotlin_test_junit

            implementation dep.log4j
            implementation dep.logback
            implementation dep.mockk
        }

        jsMain.dependencies {
            implementation dep.kotlin_stdlib_js
        }

        jsTest.dependencies {
            implementation dep.mockk_js
            implementation dep.kotlin_test_js
        }
    }

    dokka {
        impliedPlatforms = ["Common"] // This will force platform tags for all non-common sources e.g. "JVM"
        kotlinTasks {
            // dokka fails to retrieve sources from MPP-tasks so they must be set empty to avoid exception
            // use sourceRoot instead (see below)
            []
        }
        sourceRoot {
            // assuming there is only a single source dir...
            path = sourceSets.commonMain.kotlin.srcDirs[0]
            platforms = ["Common"]
        }
        sourceRoot {
            // assuming there is only a single source dir...
            path = sourceSets.jvmMain.kotlin.srcDirs[0]
            platforms = ["JVM"]
        }
    }
}