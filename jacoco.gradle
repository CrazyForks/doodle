apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.7'
}

project.afterEvaluate {
    def testTaskName = "jvmTest"

    // Create gradle task
    task "testCommonUnitTestCoverage" (type:JacocoReport, dependsOn: "$testTaskName") {
        group       = "Reporting"
        description = "Generate Jacoco coverage reports on the common module build."

        def excludes = [
            "**/*commonTest/**",
            "**/*jvmTest/**",
            "**/*jsTest/**",
            "**/*Test*.*",
            "**/io/nacular/doodle/ManualScheduler*.*",
            "**/io/nacular/doodle/event/KeyText*.*",
            "**/io/nacular/doodle/event/KeyCode*.*",
        ]

        getClassDirectories().from(fileTree(
            dir     : "${project.buildDir}/classes/kotlin/jvm/",
            excludes: excludes
        ))

        def coverageSourceDirs = [
            "src/commonMain/kotlin",
            "src/jsMain/kotlin"
        ]

        getAdditionalSourceDirs().from(files(coverageSourceDirs))
        getSourceDirectories   ().from(files(coverageSourceDirs))
        getExecutionData       ().from(files("${project.buildDir}/jacoco/jvmTest.exec"))

        reports {
            xml.required.set (true)
            html.required.set(true)
        }
    }
}